buildscript {
    ext {
        mockitoVersion = '5.11.0'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id "io.freefair.lombok" version '8.6'
    id 'application'
    id 'jacoco'
    id "org.owasp.dependencycheck" version '8.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id "com.diffplug.spotless" version "6.25.0"
}

group 'nl.fairspace'
version System.getenv('VERSION') ?: '0.0.1-SNAPSHOT'

compileJava {
    sourceCompatibility = 21
    targetCompatibility = 21
}

application {
    mainClassName = "nl.fairspace.pluto.PlutoApplication"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.thehyve.nl/content/repositories/fairspace-releases/" }
    maven { url "https://repo.thehyve.nl/content/repositories/fairspace-snapshots/" }
}

ext {
    springCloudVersion = '2023.0.0'
    jacksonVersion = '2.16.1'
}

jacoco.toolVersion = "0.8.11"

dependencies {
    implementation("com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}")
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.cloud:spring-cloud-gateway-mvc')
    implementation('org.springframework.session:spring-session-core')
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation("org.springframework.cloud:spring-cloud-starter-gateway")
    implementation('org.springframework.boot:spring-boot-actuator')

    implementation 'com.nimbusds:nimbus-jose-jwt:9.37.3'
    implementation 'com.nimbusds:oauth2-oidc-sdk:11.10.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'org.apache.commons:commons-lang3:3.14.0'
    testImplementation 'commons-io:commons-io:2.15.1'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

springBoot {
    buildInfo()
}

spotless {
    java {
        palantirJavaFormat()

        importOrder('java|javax', '', 'nl.fairspace', '\\#nl.fairspace', '\\#')
        removeUnusedImports()

        trimTrailingWhitespace()

        toggleOffOn()
    }
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = true
    }
}

dependencyCheck {
    failBuildOnCVSS = 9
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
