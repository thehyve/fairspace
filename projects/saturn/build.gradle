buildscript {
    ext {
        jena_version = '5.0.0'
        milton_version = '4.0.2.2101' // Milton >= 4 is migrated to Jakarta EE 9, which is not compatible with Jena < 5 (no stable release of Jena 5 yet). To be updated when Jena 5 is released.
        mockitoVersion = '5.11.0'
        jacksonVersion = '2.15.3' // check what version is used by Jena
        postgresqlVersion = '42.7.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id "io.freefair.lombok" version "8.6"
    id 'application'
    id 'jacoco'
    id "org.owasp.dependencycheck" version "8.2.1"
    id "com.diffplug.spotless" version "6.25.0"
}

group 'io.fairspace'
version '1.0-SNAPSHOT'

compileJava {
    sourceCompatibility = 21
    targetCompatibility = 21
}

application {
    mainClassName = "io.fairspace.saturn.SaturnApplication"
    applicationDefaultJvmArgs = ['-XX:+ShowCodeDetailsInExceptionMessages']
}

repositories {
    mavenCentral()

    maven {
        url "https://dl.bintray.com/milton/Milton"
    }
}

lombok.version = "1.18.32"

jacoco.toolVersion = "0.8.11"

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation "org.apache.jena:jena-text:${jena_version}"
    implementation "io.milton:milton-server-ce:${milton_version}"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

    implementation "org.zoomba-lang:spark-core:3.0" // todo: to be replaced with Spring MVC
    implementation 'com.pivovarit:throwing-function:1.5.1'
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation('com.io-informatics.oss:jackson-jsonld:0.1.1') {
        exclude group: 'com.github.jsonld-java'
    }
    implementation('org.bouncycastle:bcprov-jdk15on:1.70')

    implementation 'org.apache.logging.log4j:log4j-layout-template-json:2.23.0'

    implementation 'org.keycloak:keycloak-admin-client:23.0.7'
    implementation 'org.keycloak:keycloak-policy-enforcer:23.0.7'

    implementation "org.postgresql:postgresql:${postgresqlVersion}"
    implementation "com.zaxxer:HikariCP:5.1.0"

//    runtimeOnly 'org.apache.logging.log4j:log4j-api:2.23.0'
//    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.23.0'
//    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.0'

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.testcontainers:postgresql:1.19.6"
    testImplementation('com.github.stefanbirkner:system-rules:1.19.0') {
        exclude group: 'junit', module:'junit-dep'
    }

    constraints {
//        implementation('com.fasterxml.jackson.core:jackson-databind:2.9.10.1') {
//            because 'previous versions have security vulnerabilities'
//        }
    }
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = true
    }
}

spotless {
    java {
        palantirJavaFormat()

        importOrder('java|javax', '', 'io.fairspace', '\\#io.fairspace', '\\#')
        removeUnusedImports()

        trimTrailingWhitespace()

        toggleOffOn()
    }
}

dependencyCheck {
    failBuildOnCVSS=9
    suppressionFile='suppressions.xml'
}

test {
    // From JVM 16 there are stricter rules on JDK internal access
    // They also blocks usage of testing library mocking environment variables,
    // That is why this additional arg for tests is needed
    jvmArgs = ['--add-opens', 'java.base/java.util=ALL-UNNAMED']
}
